plugins {
    id "org.jetbrains.kotlin.jvm" version "1.8.20"
    id "java"
    id "maven-publish"
    id "signing"
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}


repositories {
    mavenCentral()
    mavenLocal()
}


dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.8.20'

    testImplementation 'io.kotest:kotest-assertions-core:5.4.2'
}




//http://stackoverflow.com/questions/34377367/why-is-gradle-install-replacing-my-version-with-unspecified
group 'com.github.holgerbrandl'
version '1.1'

java {
    withJavadocJar()
    withSourcesJar()
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)

            pom {
                name = 'kutils'
                description = 'A collection of kotlin utilities'
                url = 'https://github.com/holgerbrandl/kutils'
                licenses {
                    license {
                        name = 'BSD-2'
                        url = 'https://github.com/holgerbrandl/kutils/blob/master/LICENSE.txt'
                    }
                }
                developers {
                    developer {
                        id = 'holgerbrandl'
                        name = 'Holger Brandl'
                        email = 'holgerbrandl@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:github.com/holgerbrandl/kutils.git'
                    developerConnection = 'scm:git:ssh://github.com/holgerbrandl/kutils.git'
                    url = 'https://github.com/holgerbrandl/kutils'
                }
            }
        }
    }
}


nexusPublishing {
    repositories {
        sonatype {
            snapshotRepositoryUrl = uri(project.findProperty("sonatypeStagingProfileId") ?: 'not_defined')
            username = project.findProperty("ossrhUsername")  ?: 'not_defined'
            password = project.findProperty("ossrhPassword") ?: 'not_defined'
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

